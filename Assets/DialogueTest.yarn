title: Start
tags:
position: -208,-211
---
//Act 1

//Phoebe talk
<<declare $spokenToWindy = false as bool>>
<<declare $WindyOpeningRun = false as bool>>
<<declare $spokenToByronFighter = false as bool>>
<<declare $spokenToFenFighterTavern = false as bool>>
<<declare $gardenFoundFighter = false as bool>>
<<declare $spokenToHeloiseFighter = false as bool>>
<<declare $spokenToHildegardFighter = false as bool>>
<<declare $spokenToGrimboldeFighter = false as bool>>
<<declare $hedgehogRun = false as bool>>
<<declare $spokenToHytheFighter = false as bool>>
<<declare $alteaSpineDone = false as bool>>
<<declare $spokenToZoraFighter = false as bool>>

//Fen talk
<<declare $alteaSceneFenDone = false as bool>>
<<declare $spokenToWindyDiviner = false as bool>>
<<declare $spokenToByronDiviner = false as bool>>
<<declare $spokenToAmosDiviner = false as bool>>
<<declare $spokenToZoraDiviner = false as bool>>
<<declare $spokenToHytheDiviner = false as bool>>
<<declare $spokenToGrimboldeDiviner = false as bool>>
//Act 2
<<declare $spokenToHeloiseDiviner = false as bool>>
<<declare $spokenToHildegardDiviner = false as bool>>
===
title: IntroWalk1
---
//title screen clears, Phoebe stands as the fog clears
//first screen camera transition
<i>This was a ravine, a moment ago.</i>
<i>But it's not anymore.</i>
//<<turnOffCollider IntroDialogue1>>
<<stop>>
===
title: IntroWalk2
---
<i>This place smells like a room that hasn't been opened in years.</i>
//<<turnOffCollider IntroDialogue2>>
<<stop>>
===
title: FlytrapWait
---
//abyssal flytrap, but wait button is pressed
//wait button activates the collider to assign this node
<i>The air around the plant stinks of sour milk and oysters, the soil dark, watered by the goop from the plant's own mouth.</i>
<i>It's not paying you any attention, and lets you pass.</i>
//<<turnOffCollider FlytrapDialogue>>
<<stop>>
===
///THIS IS FOR TESTING TIMELINE YARNCOMMANDS
title: TimelineTest
---
Cube: Hi
<<timelineGo Timeline>>
Sphere: Hi
<<stop>>
===
title: HedgehogRun
---
<<if $charPOV is "Fighter">>
    <<timelineGo CreatureRunCollider>>
    <<set $hedgehogRun to true>>
    <<revealCreature Windy>>
    <<stop>>
<<endif>>
===
title: Opening
position: -223,-93
---
<<if $charPOV is "Fighter" and $hedgehogRun is true>>
        <<wait 4>>
        Windy: Oh. A newbie, sword and all.
        Windy: This place swallowed another one, huh?
        <<set $WindyOpeningRun to true>>
    <<stop>>
<<endif>>
<<if $WindyOpeningRun is true>>
    <<stop>>
<<endif>>
===
title: Windy
position: -172,85
---
//<<if $WindyOpeningRun is false && $spokenToWindy is false>>
    //<<runOpening YarnManager>>
    //<<set $WindyOpeningRun to true>>
    //<<jump Opening>>
    //<<stop>>
//<<if $charPOV is "Fighter">>
<<if $charPOV is "Fighter" and $spokenToWindy is false>>
    Windy: Wow. Another newbie. 
    //And here I thought Altea was the last for a while.
    Windy: Damn. Guess that means I lose the bet.
    Windy: How'd you get here, newbie?
    //The Fighter: <i>You say nothing.</i>
    The Fighter: ...
    Windy: Oh, not the talkative type? That's fine.
    Windy: Ugh, the Moon's going to be brighter soon, and I'm too far out.
    Windy: Did you see where that little pest went? It took my trowel.
    //The Fighter: <i>You shake your head.</i>
    The Fighter: ...
    Windy: Drat. And I only just found that trowel.
    Windy: The ruins by the well are no picnic, trust me. It's like a breeding ground for the axe-handed skinks.
    Windy: Although they don't breed. At least, I don't think they do.
    Windy: Anyway. Do you know how you got here? 
    Windy: Do you even know where here is?
    //The Fighter: <i>You say nothing.</i>
    The Fighter: ...
    Windy: Still nothing, huh?
    Windy: Okay, well, you should talk to Fen. 
    Windy: She's the only one who's ever ended up here on purpose.
    Windy: Unless... you did too?
    Windy: Nevermind, I doubt it. 
    Windy: You seem like someone who would fall down a ravine and end up here.
    Windy: Fen'll help you get sorted out. If you can stand her yammering. 
    Windy: She's at the tavern.
    Windy: And get to the bonfire if you start feeling queasy. They all feel queasy at first.
    Windy: Well, not me, but other people usually get queasy.
    Windy: Talk to me again if you get lost or confused or whatever.
    Windy: Oh, and don't look down. There's nothing down there but the sun, deep, deep down.
    Windy: All the way under the water.
    <<set $spokenToWindy to true>>
    <<stop>>
    <<endif>>
<<if $charPOV is "Fighter" and $spokenToWindy is true>>
    Windy: Fen's at the tavern.
    Windy: If you see Zora there, don't tell her I owe her, okay?
    Windy: Now, where did that little varmint go?
    <<stop>>
<<endif>>

<<if $charPOV is "Diviner" and $spokenToWindyDiviner is false>>
    Windy: Hey, I've been wondering.
    Windy: Is it true what they're saying?
    Fen: What do you mean? That you're divorced?
    Windy: That's blatantly untrue and I don't know who started that rumor.
    Windy: But no, that's not what I mean.
    //Windy: People are saying you're losing your mind.
    Windy: People are saying you're...
    Windy: Forgetting things.
    Fen: ...
    Fen: Where did you hear that? Altea?
    Windy: I shan't say.
    Windy: But she did kind of start it.
    Windy: She called you out-of-touch.
    Windy: Mired in the past that's slipping away from you.
    Windy: You know, because you're <i>immortal</i>, or whatever.
    Windy: Her words.
    Windy: People are wondering if it's true.
    Fen: I was there for the First Bastion Fire, and I recall the aftermath of the Second, and the rebuilding efforts for both.
    //Windy: I've never been to Bastion, if that's even a place.
    Fen: I remember the death of the Saint of Radiant Sorrow. I remember seeing Her forces gain influence across the continent.
    Fen: So what if I don't remember the name of the village I came from? I remember where it was.
    Fen: Or... that it was off of a marsh. That I remember.
    Fen: ...
    Windy: Hmm.
    Fen: I should go. I have... things to do.
    <<set $spokenToWindyDiviner to true>>
    <<stop>>
<<endif>>

<<if $charPOV is "Diviner" and $spokenToWindyDiviner is true>>
    Windy: Look, I'm sorry, I was harsh.
    Windy: You're just the only grounded one here.
    Windy: No one wants to see you turn.
    <<stop>>
<<endif>>
===
title: VineTutorial
---
<i>Remember, you can sing by pressing enter and following the notes on your knife.</i>
===
title: Byron
position: 209,-41
---
//<<if $charPOV is "Fighter">>
<<if $charPOV is "Fighter" and $spokenToByronFighter is false>>
    Byron: Hey, you! You're breathing!
    Byron: The Beneath caught a fresh one! No one breathes around here.
    Byron: Besides Altea, of course. But she's more new, too.
    //Been a while, not since Altea!
    //Byron: Breathing, the both of you—not been here long enough not to, eh?
    Byron: Have you noticed you don't have to blink anymore?
    Byron: Or relieve yourself? Nothing created nor destroyed, as they say...
    Byron: I'm too far from the Bonfire now, but they need kindling for the fire. Or the moon will hatch, and consume us all...
    Byron: And we wouldn't want that.
    Byron: Who knows what would happen if that ever-present moon fell out of the sky.
    Byron: I'd like to see it hatch, but I'll be a husk long before then, won't I?
    Byron: And then Lady Altea will have to take care of me!
    Byron: Oh, what a punishment that would be for her, but what a pleasure for me!
    Byron: <i>He dissolves into manic laughter.</i>
    <<set $spokenToByronFighter to true>>
    <<stop>>
    <<endif>>
<<if $charPOV is "Fighter" and $spokenToByronFighter is true>>
    //Byron: Fen's a fool if they think anyone's getting into that garden anytime soon.
    Byron: There's a garden on the other side of the campfire, near the tavern...
    Byron: It would make good kindling.
    Byron: Too bad Fen won't let anyone take a hatchet to it.
    Byron: A real shame, that.
    <<stop>>
    <<endif>>

<<if $charPOV is "Diviner" and $spokenToByronDiviner is false>>
    Byron: Oh. It's you.
    Byron: Go bother someone else with your chattering.
    Byron: It's like your teeth are loose in that skull of yours.
    Byron: Someone ought to fix that.
    Fen: Like Altea, you mean?
    Byron: My Lady is as beautiful as she is merciless, it's true.
    Byron: I'd recommend her certainly as candidate.
    Fen: ...
    Fen: Thanks for the input, Byron. 
    Fen: By the way, those are bones. Not sticks.
    Byron: You must be going mad then because these are the best pieces of kindling the Beneath has to offer.
    Fen: Sure they are.
    <<set $spokenToByronDiviner to true>>
    <<stop>>
<<endif>>
<<if $charPOV is "Diviner" and $spokenToByronDiviner is true>>
    Fen: By the way, you ought to comb your hair.
    Fen: You look like an ancient mariner.
    Byron: Why thank you.
    Fen: Not a compliment.
    Fen: You look like you'd eat your compatriots if your ship was stuck on the ice for too long.
    Byron: Who's to say I haven't?
    <<stop>>
    <<endif>>
===
title: SilentTree
position: 219,-31
---
<<if $charPOV is "Fighter">>
Tree: <i>It doesn't appear that this tree has anything to say to you.</i>
<<stop>>
<<endif>>

<<if $charPOV is "Diviner">>
Tree: I sing through my roots deep within the earth to my sister the willow up on her hill,
Tree: I her graceless sibling.
Fen: Oh, don't say that. You're very graceful.
Tree: ...
Tree: Without blood I blush to bedrock.
<<stop>>
<<endif>>
===
title: TrowelGuy
---
<<if $charPOV is "Fighter">>
Creature with a Trowel: .olleh
//Creature with a Trowel: if you sing at the garden hedge, something nice will happen!
Creature with a Trowel: .llih a pu nedrag a si ereht rehtag seno llat eht erehw raen 
//near where the tall ones gather there is a garden up a hill
Creature with a Trowel: !neppah lliw ecin gnihtemos egdeh nedrag eht ta gnis uoy fi
Creature with a Trowel: !em tsurt 
Creature with a Trowel: !gniht siht elots i
<<stop>>
<<endif>>

<<if $charPOV is "Diviner">>
Fen: Hello, you.
Creature with a Trowel: !yadot tfeht dettimmoc i
Fen: What's that you have there?
Fen: Can I have it?
Creature with a Trowel: !ti gniyrrac fo derob ma i sey!
//revealsprite of trowel
//splat noise
//get rid of sprite in creature's mouth
Fen: Ew. It's covered in spit.
Fen: ...Thank you, friend.
Creature with a Trowel: !sgniht rehto laets ot gniog ma i
Fen: ...
Fen: I have no idea what you just said but I hope you have fun.
<<stop>>
<<endif>>
===
title: Verity
position: 229,-21
---
<<if $charPOV is "Fighter">> ///if the player is being Phoebe
Verity: There's no point in fighting it. Any of it.
Verity: Even the monsters. Killing them does nothing.
Verity: They're stuck here, same as us.
Verity: Might as well just melt into the Fields of Sorrow before this place can turn me itself.
<<stop>>
<<endif>>

<<if $charPOV is "Diviner">>
Fen: Everything alright, Verity?
Verity: It didn't work over there, but maybe it'll work here.
Verity: It has to.
Fen: Are you still trying for petrification?
Verity: ...
Verity: No comment.
<<stop>>
<<endif>>
===
title: Zora
position: 239,-11
---
<<if $charPOV is "Fighter" and $spokenToZoraFighter is false>>
//Zora: This is the tavern. If you want to eat or sleep, this is the place.
//Zora: Between you and me, I don't think any of us need to sleep, anymore.
Zora: Hey, welcome to the tavern!
Zora: Want anything? We've got... really old mead.
Zora: It won't get you drunk, though. Nothing will, anymore.
Zora: And people don't need to sleep, either, just so you don't panic later.
Zora: But some people still pretend they can.
Zora: There's probably some kind of bed free, but I wouldn't recommend it.
Zora: It's a lot easier to just accept that your body is on pause, forever.
<<set $spokenToZoraFighter = true>>
<<stop>>
<<endif>>

<<if $charPOV is "Fighter" and $spokenToZoraFighter is true>>
Zora: While you were out, did you see Windy?
Zora: Gray hair, kind of rude, probably berating some creature for existing in her presence?
Zora: Did she say anything about a bet we had?
Zora: Nevermind. She'll never pay up. She never does.
<<stop>>
<<endif>>

<<if $charPOV is "Diviner" and $spokenToZoraDiviner is false>>
Zora: What'll it be, Fen?
Fen: Oh, nothing.
Zora: Fine by me. Nothing is what we have.
<<set $spokenToZoraDiviner to true>>
<<stop>>
<<endif>>
===
title: Amos
position: 249,-1
---
<<if $charPOV is "Fighter">>
Amos: You see her, over there? In the green?
Amos: Wait, no, don't be obvious!
Amos: That's <i>Fen</i>.
Amos: You know, <i>Fen the Diviner</i>?
Amos: Seriously, do you live under a rock? Fen's a legend.
Amos: I've always wondered how she got here. Word has it she convinced the Beneath to let her in.
Amos: On <i>purpose</i>!
<<stop>>
<<endif>>
<<if $charPOV is "Diviner" and $spokenToAmosDiviner is false>>
Amos: You never told me how you got here, you know.
Fen: Oh, well. It's a long story.
Fen: I was looking for something.
Amos: Did you find it?
Fen: ...I'm not sure yet.
Amos: What kind of thing was it? 
Amos: ...Wedding ring? Lover's tiara?
Fen: Oh, darling. 
Fen: Just a personal possession of mine, that's all.
Amos: I'm sorry for all the questions, I'm just a little starstruck.
Fen: It's quite alright, dear. Everyone is.
<<set $spokenToAmosDiviner = true>>
<<stop>>
<<endif>>
<<if $charPOV is "Diviner" and $spokenToAmosDiviner is true>>
Amos: Altea will come around. I'm sure of it.
Amos: You've been trying so hard.
Fen: I've just been trying to make her see the value of community, you know?
Amos: Absolutely.
Fen: You understand, don't you?
Amos: Absolutely I do.
<<stop>>
<<endif>>
===
title: FenTavern
position: 259,9
---
//<<if $charPOV is "Fighter">>
<<if $spokenToFenFighterTavern is false>>
<<changeCam FenTavernManager>>
The Fighter: <i>Before you is a small but strangely imposing figure, sitting alone.</i>
The Fighter: <i>Though she is small, she has gravity, and sitting here by herself, one can almost pretend this is normal.</i>
Strangely Congenial Person: I haven't seen you before, have I?
Strangely Congenial Person: Isn't this a strange place in which we find ourselves? And to think, I meant to come here.
Strangely Congenial Person: It'd be silly, if things didn't just sit here in stasis and rot.
Fen: I'm Fen. What's your name?
The Fighter: <i>You are not comfortable sharing this yet.</i>
The Fighter: ...
Fen: Oh, you're a shy one! Wouldn't think so with your weaponry.
Fen: But I'll bet you get that a lot.
Fen: I'm getting off topic, you're probably very confused and disoriented.
Fen: Where to start...
Fen: Have you met Altea?
Fen: She's the other new one, aside from you. New-ish.
Fen: Well, I should say "us." I have not been here very long either.
Fen: She's... prickly. But she's a cleric of the order of the Saint of Radiant Sorrow, so that's nice.
Fen: A nice violent sect. She caused a coup in her own hometown, so I heard!
The Fighter: ...
<<wait  0.5>>
Fen: Do you know, sometimes I wonder about how long we'll stay here.
Fen: Because, well, the reality is that we're stuck here.
Fen: They call it the Beneath, this space between realities.
Fen: It's where lost things go. I'm afraid that includes you, now.
Fen: <i>She pauses, then brightens.</i>
Fen: But might as well make the most of it, mustn't we! No use moping like Altea.
Fen: In such a space where there should be no life, the flora and fauna are teeming!
Fen: I have a theory about it, actually, but it's a bit morbid. I'll refrain from sharing it.
The Fighter: ...
<<wait  1>>
The Fighter: <i>It's clear that Fen cannot help herself.</i>
Fen: Well, so with people-types like us, 
Fen: the longer we remain here the more hollowed out we become.
Fen: I believe that our life force is being drawn up and into the very atmosphere to fuel this place.
Fen: Maybe even to fuel a celestial body, in this place that should be subterranean, 
Fen: but where the moon sits low and never sets...
The Fighter: ...
Fen: Oh, but I'm boring you. I get caught up in these things.
Fen: I'm studying this place; I'm a diviner, you see.
Fen: I'm trying to divine its secrets.
The Fighter: <i>You chuckle, but say nothing.</i>
Fen: Say, do you sing?
Fen: I've noticed quite a few people who end up down here are musicians.
Fen: I've wondered if this world gets louder when it turns them.
Fen: By eating musicians, the Beneath takes their knack for music and makes it into its own sonic resonance,
Fen: or something like that. It's a theory I've been nursing.
Fen: You have no instrument, so I thought I'd see if you fit into the pattern.
<<wait  1>>
//Fen: Some are too talkative to appreciate it, but others are more sensitive.
Fen: In case you do sing, I found this the other day. It looks like sheet music, almost.
Fen: Here, maybe you'll have a use for it.
<<playKnock GardenCutsceneManager>>
//do we need this text?
//The Fighter: <i>She hands you a knife with music etched into the blade.</i>
//The Fighter: <i>Strangely, it looks similar to the one you already have.</i>
//GIVES PHOEBE SONG OF BEASTS
//the clink from the gardenscene:
<<getSongofBeasts InventoryManager>>
<<revealCreature FenTavernManager>>
<<wait  2>>
<<disappearCreature FenTavernManager>>
Fen: Windy found it by the garden, but she's given up music.
Fen: People think it's because she's divorced, but she denies it.
Fen: Consider it a welcome gift!
Fen: The garden's up the hill, just outside, the way you came in. 
Fen: I go up there sometimes to think.
Fen: Some of the plants up there respond to music, if you feel so inclined to try.
Fen: Let me know if anything interesting happens!
//Fen: Maybe something around here will respond to it!
<<changeBackCam FenTavernManager>>
<<set $spokenToFenFighterTavern to true>>
<<stop>>
<<endif>>

//<<if $spokenToFenFighterTavern is true>>
<<if $charPOV is "Fighter" and $spokenToFenFighterTavern is true>>
Fen: You really ought to take a look at the garden, up that very tall hill.
Fen: Even just to take in the view.
Fen: Trust me. I know these things.
Fen: I'm a <i>diviner</i>.
<<stop>>
<<endif>>
===
title: Tree
position: 269,19
---
Tree: The soil may have forgotten the sun,
Tree: but it forgets not the feel of footsteps upon it.
<<stop>>
===
title: Heloise
position: 279,29
---
<<if $charPOV is "Fighter" and $spokenToHeloiseFighter is false>>
Heloise: Welcome to our little slice of hell.
Heloise: We gather here to pretend it's night, and that food still appetizes us.
Heloise: Come back here when you're ready for "Dinner".
<<set $spokenToHeloiseFighter to true>>
<<stop>>
<<endif>>

<<if $charPOV is "Fighter" and $spokenToHeloiseFighter is true and $alteaSceneFenDone is false>>
Heloise: Oh, you're ready?
Heloise: We're not.
Heloise: Come back later.
<<stop>>
<<endif>>

//<<if $alteaSceneFenDone is true>>
<<if $charPOV is "Fighter" and $alteaSceneFenDone is true>>
Heloise: You're back.
Heloise: You found something to amuse yourself with, I take it.
Heloise: Good.
Heloise: Everyone should, eventually.
//Heloise: Or you end up like Verity, whining alone in a field.
Heloise: If you're ready, look into the fire. It's the only real ritual we have.
<<stop>>
<<endif>>

<<if $charPOV is "Diviner" and $spokenToHeloiseDiviner is false>>
Heloise: Have you convinced Altea to join us tonight?
Fen: ...No. Not yet.
Heloise: Then Liesel owes me a tooth.
<<set $spokenToHeloiseDiviner to true>>
<<stop>>
<<endif>>

<<if $charPOV is "Diviner" and $spokenToHeloiseDiviner is true>>
Fen: Why do you need the teeth, Heloise? 
Heloise: Oh, I don't know. I just like them.
Heloise: I like holding them in my hands and looking at them.
Fen: This isn't the first tooth bet I've heard you make.
Heloise: They're wasted on Liesel.
Fen: If you say so.
Heloise: She doesn't appreciate them like I do.
<<stop>>
<<endif>>
===
title: Hythe
position: 289,39
---
<<if $charPOV is "Fighter" and $alteaSceneFenDone is false and $spokenToHytheFighter is false>>
Hythe: You've seen them, then? The husks?
Hythe: These are old ones, I think. They're basically the wall now.
Hythe: And this fellow next to me is in the middle of the process.
Hythe: Most of them are harmless. Used to be like us--lost souls.
Hythe: Altea helps them, after they turn. Poor bastards.
<<set $spokenToHytheFighter to true>>
<<stop>>
<<endif>>

<<if $charPOV is "Fighter" and $alteaSceneFenDone is false and $spokenToHytheFighter is true>>
Hythe: I like sitting out here and watching the husks.
Hythe: They don't do much, but that's okay.
Hythe: I like talking to them. I can say whatever I want to them and they never react.
Hythe: I can even scream at them if I want to, and they have to just sit there.
Hythe: Nature is beautiful, don't you think?
<<stop>>
<<endif>>

<<if $charPOV is "Fighter" and $alteaSceneFenDone is true>>
Hythe: Oh, is it time for the Bonfire already?
Hythe: I think I'd much rather observe this fellow, to tell you the truth.
//Hythe: I've been observing this fellow. It hasn't moved.
Hythe: Fen says they fuse with the earth...
Hythe: Do you think that's true?
Hythe: What do you think Altea does for them?
Hythe: Do you think it's painful?
Hythe: ...Do you think she'd allow an audience?
<<stop>>
<<endif>>
===
title: Grimbolde
position: 299,49
---
<<if $charPOV is "Fighter" and $alteaSceneFenDone is false and $spokenToGrimboldeFighter is false>>
Grimbolde: Oh. Hello. Haven't seen you before.
Grimbolde: I hide out here by the road to the old bathhouse to get some goddamned peace and quiet.
Grimbolde: Except I can't get that, because the creatures use it for whatever vile frolicking they do in their spare time.
Grimbolde: When they're not skittering around and shrieking.
Grimbolde: And over in that building there, in the stables, there's all these horrible crashes at random intervals.
Grimbolde: But I can't go anywhere else because the very bones of this place make noise.
Grimbolde: It's horrible. You're going to hate it.
<<set $spokenToGrimboldeFighter to true>>
<<stop>>
<<endif>>

<<if $charPOV is "Fighter" and $alteaSceneFenDone is false and $spokenToGrimboldeFighter is true>>
Grimbolde: If anyone tells you Windy and I are divorced, it's not true.
Grimbolde: I've never been married.
Grimbolde: I'm a bachelor for life.
Grimbolde: And I'm not interested in being set up. Got it?
<<stop>>
<<endif>>

//AFTER ALTEA AND FEN SCENE
//<<if $gardenFoundFighter is true and $charPOV is "Fighter">>
//<<if $alteaSceneFenDone is true>>
<<if $charPOV is "Fighter" and $alteaSceneFenDone is true and $spokenToGrimboldeFighter is true>>
Grimbolde: Thank goodness, it's time to gather at the Bonfire.
Grimbolde: No creatures allowed down there.
Grimbolde: That's all I want. Blissful, blissful silence.
Grimbolde: Except that at "Dinner," Fen's always talking.
Grimbolde: It's hell, everywhere, all the time.
Grimbolde: Isn't it awful?
<<stop>>
<<endif>>

<<if $charPOV is "Diviner" and $alteaSceneFenDone is false and $spokenToGrimboldeDiviner is false>>
Grimbolde: Oh, Fen, can something be done about these things?
Grimbolde: I hate them.
Grimbolde: Their voices scrape my mind like nails on slate.
Fen: Hmm...
Fen: Have you tried pushing them out of the way?
Grimbolde: ...
Grimbolde: Altea's in the stables.
Grimbolde: She locked the doors hours ago. 
Grimbolde: I wouldn't think she was in there at all if not for the light.
<<set $spokenToGrimboldeDiviner = true>>
<<stop>>
<<endif>>

<<if $charPOV is "Diviner" and $alteaSceneFenDone is false and $spokenToGrimboldeDiviner is true>>
Grimbolde: She won't come out.
Grimbolde: She never comes out.
Fen: I'll do my best to convince her.
Grimbolde: But can you get her to stop that racket?
Grimbolde: It's not happening right now, but it'll happen again. It always does.
<<stop>>
<<endif>>
===
title: BabyNPC
---
Puppet: My name's Baby.
Puppet: Don't laugh.
Puppet: Now go away.
<<stop>>
===
title: OutsideGardenNPC
position: 309,59
---
<<if $charPOV is "Fighter" and $alteaSceneFenDone is false and $spokenToHildegardFighter is false>>
Hildegard: Did Fen tell you to find me?
Hildegard: I'm alright, really. I'm just listening.
Hildegard: I'm waiting for the trees to sing.
<<set $spokenToHildegardFighter to true>>
<<stop>>
<<endif>>

<<if $charPOV is "Fighter" and $alteaSceneFenDone is false and $spokenToHildegardFighter is true>>
Hildegard: Did you come here alone?
Hildegard: I came with my brother. He... he loved music.
Hildegard: ...
Hildegard: I can almost hear his favorite song in the chorus of the cliffs.
Hildegard: If I listen hard enough.
<<stop>>
<<endif>>

<<if $charPOV is "Fighter" and $alteaSceneFenDone is true>>
//<<if $spokenToHildegardFighter is true>>
Hildegard: Oh, the Bonfire...
Hildegard: I wish I could see its light from here.
Hildegard: I haven't been since my brother turned.
Hildegard: Maybe I'll... maybe I'll go later.
<<stop>>
<<endif>>

<<if $charPOV is "Diviner" and $spokenToHildegardDiviner is false>>
Hildegard: Fen, I'm feeling so peculiar.
Fen: How so, dear?
Hildegard: My head... it feels far away. And my body...
Fen: Have you mentioned this to Altea? Maybe she can help?
Hildegard: No... I feel foolish admitting this but I'm afraid of her.
Hildegard: And she never comes up here.
Fen: Well, if this strange feeling persists, tell me, okay?
Fen: And I'll talk to her for you.
Fen: She's just prickly, that's all.
Hildegard: ...Okay.
<<set $spokenToHildegardDiviner to true>>
//Hildegard: She has a beautiful singing voice.
//Fen: Don't be jealous, dear! You just have to practice.
//Hildegard: Can you ask her to teach me?
//Fen: Well... maybe if she isn't busy.
<<stop>>
<<endif>>

<<if $charPOV is "Diviner" and $spokenToHildegardDiviner is true>>
Hildegard: The moon...
Hildegard: I can hear it...
Hildegard: It's singing his favorite song.
Fen: ...are you alright, Hildegard?
Hildegard: Oh, Fen... I didn't see you.
Hildegard: I have the strangest feeling that I'll see him soon...
<<stop>>
<<endif>>
===
title: BathhouseCreature1
position: 319,69
---
<<if $charPOV is "Fighter">>
Creature: ?ylduol os sgnis noom ruo nehw nus eht rof ew evah deen tahw
<<endif>>
<<if $charPOV is "Diviner">>
Creature: <i>Hissssssss...</i>
<<endif>>
===
title: LighthouseTree
position: 329,79
---
<<if $charPOV is "Fighter">>
Tree: Do you hear it?
Tree: The song of our gestating moon?
<<endif>>
===
title: SongofBeastsTree
---
//SONG OF BEASTS TRIGGER
The Fighter: <i>This tree looks stranger than the others. Not quite right.</i>
The Fighter: <i>Mere words do not feel sufficient to communicate with it.</i>
The Fighter: <i>Maybe song will be.</i>
<i>When singing, press the up and down arrow keys to cycle through your song knives.</i>
//RUN THE SONG, THEN START TIMELINE THAT JUMPS TO THIS NODE:
//<<jump PhoebeintheGarden>>
===
title: PhoebeintheGarden
---
//Triggered by song of Beasts
<<changeCam GardenCutsceneManager>>
Creature with Many Heads: You understand it, don't you?
Creature with Fingernails: The moon is almost ready to hatch!
Creature that Looks Worried: We were like you once. Some of us were like you and we are afraid of dying.
Creature with Wings: If the moon does not hatch, it will not release any of its deaths, and there will be a dying of this world.
Creature with Many Heads: If it hoards its deaths, this world will fall to stagnation and rot and festering.
Creature with Fingernails: We will not die, but this world will be dead.
Creature that Looks Worried: You will not die, but this world will be dead.
Creature with Wings: There are things that want to eat this world when it is dead. If the moon does not hatch, they will succeed.
Creature with Fingernails: A thing that eats the sun is powerful.
Creature that Looks Worried: A thing that eats the moon eats the power of this world.
Creature with Many Heads: The moon must hatch.
Creature with Wings: Take this, and find its hatching song.
//GIVE THE MUSIC BOX
<<playKnock GardenCutsceneManager>>
<<revealCreature GardenCutsceneManager>>
<<getMusicBoxBase InventoryManager>>
<<wait  2>>
<<disappearCreature GardenCutsceneManager>>
Creature with Wings: The one who is ancient but also young will help you.
Creature with Many Heads: The one who is burned on the inside can help but may not.
Creature that Looks Worried: She does not know how to ask nicely.
//<<NotAutomatic YarnManager>>
<<changeBackCam GardenCutsceneManager>>
<<set $gardenFoundFighter to true>>
<<stop>>
===
title: FenPostGardenTransition
---
<<if $charPOV is "Diviner">>
<<wait  2>>
Fen: I feel as though I've been sitting here for ages.
Fen: I should stretch my legs.
Fen: Perhaps I'll check on Altea!
Fen: She'll likely be in the stables, I'd imagine.
<<wait  1>>
Fen: Maybe I should stop talking to myself.
<<endif>>
===
title: HighPriestessTimelineTrigger
---
<<if $charPOV is "Diviner">>
    //<<colliderActive YarnStableCutsceneSetupCollider>>
    <<timelineGo STABLETIMELINE>>
    <<jump HighPriestess>>
//<<else>>
    //<<jump HighPriestess>>
<<endif>>
===
title: HighPriestess
---
<<hideFen PlayerDiviner>>
//knock on the door
<<playKnock YarnStableCutsceneSetUpCollider>>
Fen: Altea, dear, are you in there?
<<wait  3>>
<<playKnock YarnStableCutsceneSetUpCollider>>
//knock again
Fen: I know you're likely busy, but it's time for dinner, and I was hoping you would join us tonight--
//timeline run for door opening and revealing ALtea
<<changeCam PlayerDiviner>>
<<openDoor YarnStableCutsceneSetUpCollider>>
<<revealCreature YarnStableCutsceneSetUpCollider>>
Altea: What do you want?
Fen: Well, seeing as we're all gathering at the Bonfire,
Fen: I thought I'd extend the invitation.
<<wait  1.5>>
Fen: Again.
<<wait  1.5>>
Fen: Just in case you've changed your mind?
Altea: I'm busy.
Fen: I assumed you would say that, but, Altea...
Fen: Oh, and there's a newcomer! You ought to meet her, she seems lovely.
//Fen: She's a bright one, too!
//Fen: Made her way into the garden, clever thing!
//Altea: What?
Fen: Not one for talking, but quite a lovely singer!
Altea: Someone new's here.
Fen: Yes! Isn't it exciting?
Altea: And they're not bawling like a baby?
Fen: No, she seems to be adjusting just fine!
Fen: I'm hoping she does something interesting. She has the look of someone who will do something interesting.
Fen: I have a sense about these things.
Fen: Do come and meet her, Altea. I think you'd find her as interesting as I do!
<<wait  1>>
Altea: Hm.
<<wait  2>>
Altea: No.
Fen: No?
Altea: I'm busy, Fen.
Altea: I have a lot of work to do.
Fen: But it's important to stay grounded--
Altea: It's insanity.
Altea: It's like you think pretending this is a real town,
Altea: and not a derelict ruin in what might as well be the bowels of hell,
Altea: is going to get us all out of here.
Altea: Keep us normal.
Altea: Make this place allergic to us, somehow.
Altea: But I'm not like you. I'm a realist.
Altea: This place is a hellhole,
Altea: and these husks aren't going to fix themselves.
<<wait  1>>
Fen: But--
Altea: Do you not want me to call upon my Lady to help these poor, helpless, wretched things?
Fen: Well, yes, but--
Altea: Then let me work. I'm not coming to "Dinner."
<<closeDoor YarnStableCutsceneSetUpCollider>>
<<disappearCreature YarnStableCutsceneSetUpCollider>>
<<changeBackCam PlayerDiviner>>
<<wait  1>>
//timeline for door close
Fen: Well, alright then.
<<revealFen PlayerDiviner>>
<<wait  0.5>>
<<set $alteaSceneFenDone to true>>
//BONFIRE TRIGGER ACTIVE
<<revealCreature BonfireManager>>
//switch to Phoebe
<<colliderActive FenToPhoebeManager>>
//<<disappearCreature FenGameObjectManagerBonfire>>
<<disappearCreature YarnStableCutsceneCollider>> //new addition
//<<colliderActive SwitchToPhoebeCollider>>
<<stop>>
===
title: BonfireConversation1
---
//camera change, objects change
<<changeCam BonfireManager>>
<<wait 0.5>> //change this to be shorter???
Fen: It's poetic, I think.
Fen: When you've been around as long as I have,
Fen: you start to see everything like one big pattern.
Fen: Things grow, things shrivel, things die.
Fen: There's a poetry to it. To the cycle.
//sip tea noise or animation
<<wait  1>>
Fen: Oh, you noticed the mug! 
Fen: I take it with me everywhere. I did a favor for a potter, years ago,
Fen: divined a cure for his children when they fell ill.
//Fen: predicted the health of his fledgling children after they fell ill.
Fen: Plague of the lungs, if I remember correctly.
Fen: It was everywhere back then.
Fen: I was in Bastion at the time, I think. Before the first fire.
Fen: Their sewer system was a mess then.
<<wait  0.5>>
Fen: ...Where was I?
Fen: Ah.
Fen: The potter let me mold my own tea mug, and paint it how I liked.
Fen: His youngest child liked chickens.
Fen: She had a lovely smile.
//campfire crackle is louder
<<wait  1>>
Fen: I wonder how they all turned out.
Fen: The potter's long gone, I'd imagine.
Fen: Maybe his children are still around?
<<wait  0.5>>
Fen: No need to look so sad, dear Fighter.
Fen: This is just how it is, when you're as old as I am.
Fen: You learn to grow around it.
Fen: So to speak.
//sip tea again
<<wait  2>>
Fen: Oh, dear.
Fen: This cup's gone cold.
//effects to make it look like time has passed
<<jump BonfireConversation2>>
===
title: BonfireConversation2
---
<<wait  0.5>>
<i>It's hard not to pick up other conversations, as you sit and listen.</i>
A Snobbish Voice: So I told him that I had never read it, and then he said I wasn't truly an intellectual.
A Shrill Voice: It stole my hat! It really did!
Windy: Byron's not coming, again. 
Grimbolde: What's next, another husk for Hythe to gawk at?
A Softer Voice: More tea, Fen?
Fen: Yes, thank you.
A Shrill Voice: If you see a hat with a really great feather in it...
A Snobbish Voice: It's not like any of that even matters down here. 
Grimbolde: And I heard the most dreadful noise coming from the stables...
Heloise: Everyone shut up, I can't hear Liesel being wrong.
Liesel: You just don't see how pure of heart they are!
Grimbolde: What?
Heloise: The beasts.
Liesel: They're not beasts.
<<wait  0.5>>
Fen: This is a lively bunch, Fighter, wouldn't you say?
The Fighter: ...
<<wait  1>>
//low voices in background, bonfire ambience
//teakettle over flames in this node
Fen: Say, have you seen that skeleton, over the bridge on that other island?
Fen: It's just across the water from here.
//The Fighter: <i>You shake your head.</i>
Fen: It's quite something.
//Fen: A natural marvel of this place.
//checks water
<<wait  0.5>>
//Fen: Ah, drat.
Fen: I forget sometimes how slow boiling water over an actual fire can be.
Fen: But I can't just go using spellcraft around here all willy-nilly, can I?
//The Fighter: <i>You shrug awkwardly. Fen seems amused by this.</i>
The Fighter: ...
Fen: Oh, dear, you look distressed. That was a rhetorical question.
//checks water again
//<<wait  0.5>>
Fen: Heloise, is it hot enough yet?
Heloise: Nope.
Fen: Rats.
<<wait  0.5>>
...: ...so I said, if you could turn into any of the creatures around here what would you be,
...: and he said, a tharpy, and I said that's ridiculous...
<<wait 0.3>>
//Fen: It's strange, being so old but so... miniscule compared to this place.
//Fen: It's been ages since I've felt dwarfed in age by something.
//Fen: and a plane like this... well.
//Fen: I don't have to tell you how strange this place is.
//The Fighter: <i>You say nothing.</i>
//The Fighter: ...
//The Fighter: <i>Fen seems to take this as agreement.</i>
Fen: Oh, but yes, the skeleton!
//Fen: That's something even stranger still.
Fen: To think something could once die, down here.
Fen: Could decompose, and leave remains.
Fen: Fascinating.
Fen: You don't get that, with the husks.
//Fen: As far as I am aware, the husks just... become absorbed into the terrain.
//Fen: But this creature! A leviathan of some kind,
//Fen: judging by the sheer scale of it!
//Fen: Would you like to hear the strangest part?
//<<wait  0.2>>
//The Fighter: <i>She leans in, almost conspiratorially.</i>
Fen: And it's not even the whole thing.
Fen: I've never seen the rest.
//Fen: The rest must be either lost to the sun-lake or some similar fate,
//Fen: or wherever it came from if it didn't die here.
Fen: All that's here is <i>spine</i>.
//Fen: Isn't that fascinating?
<i>You used to live in the shadow of a spine, didn't you?</i>
<i>Your hometown, built on bones.</i>
<i>Cursed bones. Furious, haunted bones.</i>
<i>Your sisters are buried with those bones now. Somewhere beneath the sand.</i>
Fen: You should go see it, Fighter.
//Fen: Wait a moment?
//<<wait  1>>
Fen: And if you see any signs of scavenging, or decomposition,
//Fen: or if the sun does anything interesting under the water,
Fen: would you let me know?
Fen: I have to wait for this damned kettle to boil,
Fen: or I'll lose track of it altogether and it'll get cold again,
Fen: and then I'll just be upset.
//Fen: ...Or night?
//Fen: Or... whatever time this is.
<<changeBackCam BonfireManager>>
<<revealCreature SpineObjectsManager>>
<<revealCreature spinePathManager>>
<<disappearCreature BonfireManager>>
//<<restart BonfireManager>>
<<stop>>
===
title: AlteaAtTheSpine
---
<<if $charPOV is "Fighter" and $alteaSpineDone is false>>
Altea: ...
<<wait  2>>
<<disappearCreature BonfireManager>>
Altea: What are you doing out here.
<i>It's not a question, but an accusation.</i>
The Fighter: ...
Altea: This is important. I can't be interrupted.
Altea: Go back to the Bonfire.
<<wait  1>>
Altea: Are you deaf? Leave me alone.
<<wait  1>>
Altea: Fen sent you here, didn't she?
Altea: She can't help meddling when it comes to the husks.
Altea: I told her I'd get results, and I'll <i>get</i> those results.
Altea: One way or another.
<<wait  2>>
Altea: You know what?
Altea: Go tell her I'm making progress.
Altea: This one's almost fused with this hellhole, anyway.
Altea: I'm doing it a favor.
<i> You take in the husk beside her, the tap sticking out of its side.</i>
Altea: Don't worry about that.
Altea: It's for the greater good, I promise you.
Altea: Now, run along.
//ALTEA LEAVES?
//<<disappearCreature PlayerClericStandInSpine>>
//OR PLAYER TURNS (camera would do something)
//CREATURE APPEARS
<<revealCreature PlayerClericStandInSpine>>
<<wait  1.5>>
<<changeCam PlayerClericStandInSpine>>
<<wait  2>>
Creature that Looks Worried: The moon must hatch.
<<playKnock CommandHolderSpine>>
<<revealCreature CommandHolderSpine>>
<<wait  2>>
<<disappearCreature CommandHolderSpine>>
//GIVE FIGURINE
<<getMusicBoxFigure InventoryManager>>
Creature that Looks Worried: Go to the bathing place.
Creature that Looks Worried: See what she has done.
<<changeBackCam PlayerClericStandInSpine>>
<<colliderActive PhoebetoFenSpineManager>>
//switch into Fen
<<set $alteaSpineDone to true>>
<<disappearCreature Hound>>
<<revealCreature FenPostBonfireMemory>>
<<stop>>
<<endif>>
===
title: BonfireTransition
---
Fen: Ah, you're back! And... with creatures in tow!
Fen: So many creatures. 
Fen: Oh, Windy will not be happy.
<<wait  1>>
Fen: Are you alright? You look... queasy.
Creature with Fingernails: She cannot understand us.
Creature with Fingernails: You have the song to help her understand.
//SONG OF BEASTS SONG
<<wait  4>>
Fen: That was lovely, but--
Creature with Wings: Old but young one!
Creature with Fingernails: The burning one is sapping them!
Fen: You can speak?
Fen: In the common tongue?
Fen: Fascinating.
Creature with Fingernails: The singing one is our friend, she understands us! 
Creature with Fingernails: But the burning one does not!
Creature with Wings: The burning one is hungry and afraid.
Creature with Fingernails: She takes the ones who were like you.
Creature with Fingernails: The ones who sit.
Creature with Wings: She has them in her boxy room, and she takes them,
Creature with Wings: and she steals what them there is left,
Creature with Wings: and she throws them away afterwards.
Creature with Fingernails: Our friend saw it.
Creature with Wings: The burning one hurts them.
Creature with Wings: What she is doing is not helping.
Creature with Fingernails: The moon is hungry, but you know that.
Creature with Wings: You know that, old but young one.
Both: The moon is hungry and hungry and hungry.
<<wait  0.75>>
//<i>Loran's Spine has followed you.</i>
//<i>Your home, long since fallen to madness and chaos and beasts,</i>
//<i>is here. The spine of it, anyway.</i>
//<i>You are haunted, and do not speak.</i>
Fen: Oh, dear.
<<wait  0.5>>
Fen: I don't quite know how to respond to all that.
Fen: Fighter, here, wait, sit down.
Fen: Can I pour you a cup of tea?
Fen: Smelling salts?
Fen: Anything?
<<wait  0.5>>
Fen: Altea's hurting them? The husks?
Fen: Why would she lie to me?
Fen: I hoped maybe...
<<wait  0.2>>
Fen: Here, I'll go.
Fen: I need to see this for myself.
Fen: Fighter, please. Sit. Relax.
Fen: Keep these creatures company. I don't know if they've seen fire this close before.
Fen: I'll investigate. It's all under control.
//POV SHIFT
<<stop>>
===
title: FenMemoryTransition
---
//at stable, looking at Altea's stuff

Fen: Oh, dear gods...
Fen: Altea, what have you done?
//HEADACHE MOMENT
<<wait  2>>
Fen: It's happening.
Fen: Oh gods, it's happening, isn't it?
Fen: I have to stay grounded.
Fen: I can't turn. I <i>can't</i>.
Fen: I'll read my old scrapbook, that's what I'll do. That always fixes me.
Fen: Jog the old memory with my old writings. That'll do it.
//SCRAPBOOK UI appear
<<wait  1>>
//Animation trigger: memories go away
Fen: No. No, no,
Fen: no no no <i>NO--</i>
Fen: Why is it gone? What's wrong with me?
Fen: Why can't I <i>remember</i>?
Fen: I have to remember. I have to stay me.
Fen: Gods, this was so much easier when I needed to breathe.
<<wait  0.5>>
Fen: I'll go to the roof of the Rookery. That's what I'll do.
Fen: I need to think.
Fen: I need to remember. I remember easier up there.
Fen: Yes. Yes, that's the way.
<<stop>>

//OLD STUFF:
//<<wait  1>>
//Fen: Things are dying down a bit...
//Fen: I should go take a walk. Look for things that make me remember.
//Fen: Gods know I need it.
//Fen: If I forget where I should go, the compass in my hand should help guide me.
//Fen: If that enchantment I cast before I got here even still works...
//<<wait  0.5>>
//Fen: That willow tree up on the roof of the rookery...
//Fen: Could I get up there again?
===
title: SparklePathIntro
---
<<if $charPOV is "Diviner">>
//timeline to play animation of sparkle
Fen: Well, now. That's curious.
<<endif>>
===
title: FenOnTheHill
---
<<if $charPOV is "Diviner">>
//timeline to change camera to look at the Moon
Fen: I know you've got it, up there.
Fen: I can feel it.
Fen: Like a buzzing in the back of my mind.
//play soundscape?
<<wait  1>>
Fen: It's almost like you're nearly at the end of your digestion, isn't it?
Fen: Or maybe it's gestation.
Fen: Maybe you <i>are</i> hatching.
<<wait  2>>
Fen: I know you've got my death up there.
Fen: I know you're hoarding the stuff.
//buzzing/pulsing Moon sound plays, maybe lighting change too, camera changes.
//Fen: Your arrogance does not deter me.
Fen: You've eaten enough.
//Fen: This place has stagnated for too long.
//hum gets louder
<<wait  1>>
Fen: I have not lived countless centuries,
Fen: traversed countless continents,
Fen: to lose the scent down here.
Fen: I know you have it.
Fen: I can feel it like a missing shadow.
Fen: It's <i>mine</i>, not some nutrients to repurpose.
<<wait  2>>
//Fen: I'm coming for it, you false moon. You hive.
Fen: I'm tired, and I'm taking it back.
<<wait  1>>
Fen: Even if it means this place crumbles.
//timeline play to black out? demo end screen?
<<endif>>
===
